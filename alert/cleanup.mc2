//
// Description : This macro remove aknowledged alerts
// Input : [ GTS ] 
// Output : [ GTS ]
//
<%
  DUP TYPEOF 'MAP' == ASSERT 'mc2bank.alert.cleanup.gts'  STORE

  // Generate map to store acks for performance
  // Each key match a unique alert computed with the macro '@MC2Bank/alert/hash'
  {} 'mc2bank.alert.cleanup.acks_m' STORE
  $mc2bank.alert.cleanup.gts 'acks' GET <% $mc2bank.alert.cleanup.acks_m SWAP DUP LABELS @MC2Bank/alert/hash PUT DROP %> FOREACH

  [
    // Run through every alert
    $mc2bank.alert.cleanup.gts 'levels' GET
    // Keep only changing state
    @MC2Bank/gt/compact_time
    <%
      'mc2bank.alert.cleanup.selected_levels' STORE
      // Compute current alert key
      $mc2bank.alert.cleanup.selected_levels LABELS @MC2Bank/alert/hash 'mc2bank.alert.cleanup.key' STORE

      // Retrieve acks matching selector (we don't use the filter framework has it's not effiscient enough here, but a map instead)
      $mc2bank.alert.cleanup.acks_m $mc2bank.alert.cleanup.key GET 'mc2bank.alert.cleanup.selected_acks' STORE 

      // If there is no acks serie for the selected alert, we return the serie.
      // Otherwise, we compute a negmask between th acks serie and the levels serie to keep only unaknowledged alerts.
      <% $mc2bank.alert.cleanup.selected_acks ISNULL %>
      <% $mc2bank.alert.cleanup.selected_levels %>
      <%
        [
          [ $mc2bank.alert.cleanup.selected_acks ]
          [ $mc2bank.alert.cleanup.selected_levels ]
          []
          op.negmask
        ] APPLY 
      %>
      IFTE
    %> FOREACH
  ]

  // Remove empty gts (all ticks have been aknowledged)
  NONEMPTY
  $mc2bank.alert.cleanup.gts SWAP 'levels' PUT
%>
