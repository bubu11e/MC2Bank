//
// Description : This retrieve all unaknowledged alerts
// Input :  read_token (For alert storage)
//          alert_filter (Map that filter alerts)
//          end (Instant of the last alert to be taken into account)
//          depth (Period to watch)
//          notify_parameters (Notify macro parameters as an array)
//          notify_macro (Notify macro to trigger for every non acknoledged alert)
// Output : [ GTS ] (One per alert with unaknowledged ticks) 
//
<%
  'notify_macro' STORE
  'notify_parameters' STORE
  'depth' STORE
  'end' STORE
  'alert_filter' STORE
  'token' STORE

  <%
    MAP-> ->MAP MSORT
    MAP-> '' SWAP TOLONG JOIN
   'UTF-8' ->BYTES
    SHA1
    ->HEX
  %> 'hash' STORE

  
  // Fetch alerts
  [ $token '~alert.(level|ack|details)' $alert_filter $end $depth ] FETCH

  // Filter differents parts of the alerts into differents vars
  DUP DUP
  // Here we compact the level part of the alert to only keep changing state
  [ SWAP [] 'alert.level' filter.byclass ] FILTER @MC2Bank/gt/compact_time 'level' STORE
  [ SWAP [] 'alert.details' filter.byclass ] FILTER 'details' STORE
  [ SWAP [] 'alert.ack' filter.byclass ] FILTER 'ack' STORE

  // Generate map to store details and acks for performance
  {} 'details_m' STORE
  {} 'ack_m' STORE
  $details <% $details_m SWAP DUP LABELS $hash EVAL PUT DROP %> FOREACH
  $ack <% $ack_m SWAP DUP LABELS $hash EVAL PUT DROP %> FOREACH

  $level
  // Running through every alert
  <%
    'selected_level' STORE
    // Retrieve alert selector
    $selected_level LABELS '.app' REMOVE DROP 'selector' STORE
    $selected_level LABELS  $hash EVAL 'key' STORE

    // Retrieve details matching selector (we don't use the filter framework has it's not effiscient enough here)
    $details_m $key GET DUP <% ISNULL %> <% DROP NEWGTS 'alert.details' RENAME $selector RELABEL %> IFT 'selected_details' STORE

    // Retrieve acks matching selector (we don't use the filter framework has it's not effiscient enough here)
    $ack_m $key GET DUP <% ISNULL %> <% DROP NEWGTS 'alert.ack' RENAME $selector RELABEL %> IFT 'selected_ack' STORE
    
    // Filter unacknoledged ticks
    [
      // Get alert ack time serie
      [ $selected_ack ]
      [ $selected_level ]
      []
      op.negmask
    ] APPLY
    
    TICKS
    
    // Running through every ticks
    <%
      'tick' STORE
      { 
        'tick' $tick 
        'alert' $selector 
        'level' $selected_level $tick ATTICK 4 GET
        'details' $selected_details $tick ATTICK 4 GET // Retrieve the value at the tick we are interested in
                  <% DUP ISNULL ! %> <% JSON-> %> IFT
      } $notify_parameters LIST-> DROP $notify_macro EVAL
    %> FOREACH
  %> FOREACH
%>
